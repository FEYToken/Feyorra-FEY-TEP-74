#include "workchain.fc";

const ONE_TON = 1000000000;

const MIN_STORAGE_DURATION = 5 * 365 * 24 * 3600; ;; 5 years

;;# Precompiled constants
;;
;;All of the contents are result of contract emulation tests
;;

;;## Minimal fees
;;
;;- Transfer [/sandbox_tests/JettonWallet.spec.ts#L935](L935) `0.028993415` TON
;;- Burn [/sandbox_tests/JettonWallet.spec.ts#L1185](L1185) `0.016032001` TON


;;## Storage

const JETTON_WALLET_BITS  = 1033;
const JETTON_WALLET_CELLS = 3;

const JETTON_WALLET_INITSTATE_BITS  = 931;
const JETTON_WALLET_INITSTATE_CELLS = 3;

;; jetton-wallet.fc#L163 - maunal bits counting
const BURN_NOTIFICATION_BITS = 754; ;; body = 32+64+124+(3+8+256)+(3+8+256)
const BURN_NOTIFICATION_CELLS = 1; ;; body always in ref

;;## Gas

const SEND_TRANSFER_GAS_CONSUMPTION    = 9465;

const RECEIVE_TRANSFER_GAS_CONSUMPTION = 10455;

const SEND_BURN_GAS_CONSUMPTION    = 5891;

const RECEIVE_BURN_GAS_CONSUMPTION = 8287;

;; croschain transfer
const CROSS_CHAIN_TRANSFER_NOTIFICATION_BITS = 1458; ;; body = 32+64+124+(3+8+256)+(3+8+256)+64+640 chain selector + external address
const CROSS_CHAIN_TRANSFER_NOTIFICATION_CELLS = 2; ;; body + externbal address

const SEND_CROSS_CHAIN_TRANSFER_GAS_CONSUMPTION = 6844;
const RECEIVE_CROSS_CHAIN_TRANSFER_GAS_CONSUMPTION = 10846;
const RECEIVE_BOUNCED_CROSS_CHAIN_TRANSFER_NOTIFICATION_GAS_CONSUMPTION = 2364;


int calculate_jetton_wallet_min_storage_fee() inline {
    return get_storage_fee(MY_WORKCHAIN, MIN_STORAGE_DURATION, JETTON_WALLET_BITS, JETTON_WALLET_CELLS);
}

int forward_init_state_overhead() inline {
    return get_simple_forward_fee(MY_WORKCHAIN, JETTON_WALLET_INITSTATE_BITS, JETTON_WALLET_INITSTATE_CELLS);
}

() check_amount_is_enough_to_transfer(int msg_value, int forward_ton_amount, int fwd_fee) impure inline {
    int fwd_count = forward_ton_amount ? 2 : 1; ;; second sending (forward) will be cheaper that first

    int jetton_wallet_gas_consumption = get_precompiled_gas_consumption();
    int send_transfer_gas_consumption = null?(jetton_wallet_gas_consumption) ? SEND_TRANSFER_GAS_CONSUMPTION : jetton_wallet_gas_consumption;
    int receive_transfer_gas_consumption = null?(jetton_wallet_gas_consumption) ? RECEIVE_TRANSFER_GAS_CONSUMPTION : jetton_wallet_gas_consumption;

    throw_unless(error::not_enough_gas, msg_value >
    forward_ton_amount +
        ;; 3 messages: wal1->wal2,  wal2->owner, wal2->response
        ;; but last one is optional (it is ok if it fails)
    fwd_count * fwd_fee +
    forward_init_state_overhead() + ;; additional fwd fees related to init-state in internal_transfer
    get_compute_fee(MY_WORKCHAIN, send_transfer_gas_consumption) +
    get_compute_fee(MY_WORKCHAIN, receive_transfer_gas_consumption) +
    calculate_jetton_wallet_min_storage_fee() );
}

() check_amount_is_enough_to_burn(int msg_value) impure inline {
    int jetton_wallet_gas_consumption = get_precompiled_gas_consumption();
    int send_burn_gas_consumption = null?(jetton_wallet_gas_consumption) ? SEND_BURN_GAS_CONSUMPTION : jetton_wallet_gas_consumption;

    throw_unless(error::not_enough_gas, msg_value > get_forward_fee(MY_WORKCHAIN, BURN_NOTIFICATION_BITS, BURN_NOTIFICATION_CELLS) + get_compute_fee(MY_WORKCHAIN, send_burn_gas_consumption) + get_compute_fee(MY_WORKCHAIN, RECEIVE_BURN_GAS_CONSUMPTION));
}

() check_amount_is_enough_to_cross_chain_transfer(int msg_value) impure inline {
    int jetton_wallet_gas_consumption = get_precompiled_gas_consumption();
    int send_cross_chain_transfer_gas_consumption = null?(jetton_wallet_gas_consumption) ? SEND_CROSS_CHAIN_TRANSFER_GAS_CONSUMPTION : jetton_wallet_gas_consumption;

    throw_unless(
        error::not_enough_gas,
        msg_value > get_forward_fee(MY_WORKCHAIN, CROSS_CHAIN_TRANSFER_NOTIFICATION_BITS, CROSS_CHAIN_TRANSFER_NOTIFICATION_CELLS)
          + get_compute_fee(MY_WORKCHAIN, send_cross_chain_transfer_gas_consumption)
          + get_compute_fee(MY_WORKCHAIN, RECEIVE_CROSS_CHAIN_TRANSFER_GAS_CONSUMPTION)
          + get_compute_fee(MY_WORKCHAIN, RECEIVE_BOUNCED_CROSS_CHAIN_TRANSFER_NOTIFICATION_GAS_CONSUMPTION)
    );
}
